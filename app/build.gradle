apply plugin: 'com.android.application'

android {
    compileSdkVersion 27

    signingConfigs {
        release {
            File propertiesFile = new File(project.property("genesisvision.properties") as String)
            Properties signProperties = new Properties()
            if (propertiesFile.exists()) {
                signProperties.load(new FileInputStream(propertiesFile))

                storeFile file(signProperties.getProperty("keystore"))
                storePassword signProperties.getProperty("keystore.password")
                keyAlias signProperties.getProperty("keyAlias")
                keyPassword signProperties.getProperty("keyPassword")
            }
        }
    }

    defaultConfig {
        applicationId "vision.genesis.clientapp"
        minSdkVersion 16
        targetSdkVersion 27
        multiDexEnabled true

        versionCode 18040301
        versionName "0.0.2-alpha"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        renderscriptTargetApi 27
        renderscriptSupportModeEnabled true

        buildConfigField "boolean", "PROD", "false"

        File propertiesFile = new File(project.property("genesisvision.properties") as String)
        Properties appProperties = new Properties()
        if (propertiesFile.exists()) {
            appProperties.load(new FileInputStream(propertiesFile))
            buildConfigField "String", "API_ADDRESS", appProperties.getProperty("apiAddress")
            buildConfigField "String", "TOURNAMENT_API_ADDRESS", appProperties.getProperty("tournamentApiAddress")
        }
    }

    buildTypes {
        debug {
            versionNameSuffix ' Dev'
            applicationIdSuffix '.dev'
        }
        release {
            buildConfigField "boolean", "PROD", "true"

            signingConfig signingConfigs.release

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "appType"

    productFlavors {
        investor {
            dimension "appType"

            resValue "string", "app_name", "Genesis Vision Investor"
            applicationIdSuffix '.investor'
        }
        manager {
            dimension "appType"

            resValue "string", "app_name", "Genesis Vision Manager"
            applicationIdSuffix '.manager'
        }
        tournament {
            dimension "appType"

            resValue "string", "app_name", "Genesis Vision Tournament"
            applicationIdSuffix '.investor'
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/DEPENDENCIES'
    }
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:27.1.0'
    implementation 'com.android.support:design:27.1.0'
    implementation 'com.android.support:recyclerview-v7:27.1.0'
    implementation 'com.android.support:support-v4:27.1.0'
    implementation 'com.android.support:multidex:1.0.3'

    implementation 'com.google.firebase:firebase-core:12.0.0'
    implementation 'com.google.firebase:firebase-crash:12.0.0'

    implementation 'com.aurelhubert:ahbottomnavigation:2.1.0'

    implementation 'ru.terrakok.cicerone:cicerone:2.0.0'

    implementation 'com.arello-mobile:moxy:1.5.3'
    implementation 'com.arello-mobile:moxy-android:1.5.3'
    implementation 'com.arello-mobile:moxy-app-compat:1.5.3'

    implementation 'com.jakewharton:butterknife:8.4.0'
    implementation 'com.jakewharton.timber:timber:4.1.0'
    implementation 'com.jakewharton.rxbinding:rxbinding-appcompat-v7:0.3.0'

    implementation 'com.google.dagger:dagger:2.11'

    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'io.reactivex:rxjava:1.3.0'

    implementation 'org.greenrobot:eventbus:3.0.0'

    implementation 'com.facebook.fresco:fresco:1.0.0'

    implementation 'joda-time:joda-time:2.9.9'

    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'

    implementation 'me.imid.swipebacklayout.lib:library:1.1.0'

    implementation 'com.github.kenglxn.QRGen:android:2.4.0'

    implementation 'com.github.hotchemi:permissionsdispatcher:3.1.0'

    implementation 'com.theartofdev.edmodo:android-image-cropper:2.6.0'

    implementation('com.wdullaer:materialdatetimepicker:3.5.1') {
        exclude group: 'com.android.support'
    }

    implementation('com.appyvet:materialrangebar:1.4.1') {
        exclude module: 'support-compat'
    }

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.11'
    annotationProcessor 'com.github.hotchemi:permissionsdispatcher-processor:3.1.0'
    annotationProcessor 'com.arello-mobile:moxy-compiler:1.5.3'

    compileOnly 'com.arello-mobile:moxy-compiler:1.5.3'
    implementation project(path: ':apiClient')
}

apply plugin: 'com.google.gms.google-services'